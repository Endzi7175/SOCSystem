//created on: May 19, 2019
package sbnz.rules
import com.sbnz.SIEMCenter2.model.LogEntry
import com.sbnz.SIEMCenter2.model.SuccessLoginLog
import com.sbnz.SIEMCenter2.model.AlarmTriggered
import com.sbnz.SIEMCenter2.model.MaliciousIpAddress
import com.sbnz.SIEMCenter2.service.AlarmTriggeredService;
import com.sbnz.SIEMCenter2.repository.MaliciousIpRepository;
import org.kie.api.time.SessionClock;
import java.util.ArrayList
global ArrayList<AlarmTriggered> alarms;
global AlarmTriggeredService alarmService;
global MaliciousIpRepository ipRepo;

rule "Vise od 9 neuspesnih prijava sa istim korisnickim imenom"
    when
        $l1: LogEntry(message.equals("Neuspesna prijava na sistem"), $m: message, $id: userId) 
        not (AlarmTriggered(customerId == $id,  type == AlarmTriggered.VISE_OD_2_PRIJAVE_ISTI_KORISNIK))
        Number(intValue >= 9) from accumulate(
            $l2: LogEntry(
                this != $l1, 
                message == $m,
                userId == $id
            ),
            count($l2)
        )
        
    then
    	alarmService.save(new AlarmTriggered($id, "3 neuspela pokusaja prijave", AlarmTriggered.VISE_OD_2_PRIJAVE_ISTI_KORISNIK));
        System.out.println("Alarm: vise od 3 neuspelih prijava od stane korisnika" + $id);
    	insert(new AlarmTriggered($id, "3 neuspela pokusaja prijave", AlarmTriggered.VISE_OD_2_PRIJAVE_ISTI_KORISNIK));
end
rule "Vise od 9 neuspesnih prijava od na istoj masini"
    when
        $l1: LogEntry(message.equals("Neuspesna prijava na sistem"),  $m: message, $machineId: machineId)
        not (AlarmTriggered(customerId == $machineId, type == AlarmTriggered.VISE_OD_4_PRIJAVE_ISTA_MASINA))
        Number(intValue >= 9) from accumulate(
            $l2: LogEntry(
                this != $l1, 
                message == $m,
                machineId == $machineId
            ),
            count($l2)
        )
        
    then
    	alarmService.save(new AlarmTriggered($machineId, "Neuspesna prijava na istoj masini", AlarmTriggered.VISE_OD_4_PRIJAVE_ISTA_MASINA));
        System.out.println("Alarm: vise od 9 neuspelih prijava na istoj masini");
    	insert(new AlarmTriggered($machineId, "Neuspesna prijava na istoj masini, id " + $machineId, AlarmTriggered.VISE_OD_4_PRIJAVE_ISTA_MASINA));
end
rule "Pojava loga koji je tipa error log level = 1 kad je error"
    when
    	
        LogEntry(logLevel == 1,id == null, $m: message, $id: userId)
    then
    	
    	alarmService.save(new AlarmTriggered($id, "Greska u programu: " + $m, AlarmTriggered.ERROR));
    	System.out.println("Alarm: greska u programu");
    	
    	insert(new AlarmTriggered($id, "Greska u programu: " + $m, AlarmTriggered.ERROR));
end
rule "Neuspesan pokusaj prijave na sistem od stane korisnika koji nije bio aktivan vise od 90 dana"
    when
        $l1 : LogEntry(message.equals("Neuspesna prijava na sistem"), $m: message, $id: userId) 
        not (AlarmTriggered(customerId == $id, type == AlarmTriggered.POKUSAJ_PRIJAVE_NEAKTIVAN_KORISNIK))
        Number(intValue > 0) from accumulate(
            $l2: LogEntry(
                this != $l1, 
                message.equals("Uspesna prijava na sistem"),
                userId == $id
                
            ),
            count($l2)
        )
        and
        Number(intValue == 0) from accumulate(
            $l2: LogEntry(
                this != $l1, 
                message.equals("Uspesna prijava na sistem"),
                userId == $id, 
                this meets[90d] $l1
            ),
            count($l2)
        ) 
        //not (LogEntry())
        //
        //not(LogEntry(message.equals("Neuspesna prijava na sistem"), userId == $id, this meets[90d] $l1))
        //$l3 : (LogEntry(message.equals("Neuspesna prijava na sistem"), userId == $id, this before[90d] $l2)
        //not (AlarmTriggered(customerId == $id))
        
    then
    	alarmService.save(new AlarmTriggered($id, "Neuspesna prijava na sistem od strane neaktivnog korisnika", AlarmTriggered.POKUSAJ_PRIJAVE_NEAKTIVAN_KORISNIK));
        System.out.println("Alarm: Neuspesna prijava na sistem od strane neaktivnog korisnika " + $id);
    	insert(new AlarmTriggered($id, "Neuspesna prijava na sistem od strane neaktivnog korisnika", AlarmTriggered.POKUSAJ_PRIJAVE_NEAKTIVAN_KORISNIK));
end
//sredi
rule "Vise od 15 neuspesnih prijava na razlicite delove informacionog sistema sa iste ip adrese"
    when
        $l1: LogEntry(message.equals("Neuspesna prijava na sistem"),  $m: message, $ip: ipAddress, $isType : informationSystemType, $ts : timeStamp)
        not (AlarmTriggered(customerId == $ip, type == AlarmTriggered.VISE_OD_15_PRIJAVA_RAZLICITI_SISTEMI))
        Number(intValue >= 14) from accumulate(
            $l2: LogEntry(
                this != $l1, 
                message == $m,
                ipAddress == $ip,
                informationSystemType !=  $isType,
                this after[0s, 5d] $l1
            ),
            count($l2)
        )
        
    then
    	alarmService.save(new AlarmTriggered($ip, "Neuspesna prijava na razlicite informacione sisteme sa iste ip adrese", AlarmTriggered.VISE_OD_15_PRIJAVA_RAZLICITI_SISTEMI));
        System.out.println("Alarm: vise od 15 neuspesnih prijava na razlicite informacione sisteme sa iste ip adrese " );
    	insert(new AlarmTriggered($ip, "Neuspesna prijava na razlicite informacione sisteme sa iste ip adrese" + $ip, AlarmTriggered.VISE_OD_15_PRIJAVA_RAZLICITI_SISTEMI));
end
rule "Neuklonjen virus"
    when
    	$clock : SessionClock()
        $l1: LogEntry(message.equals("Virus"), timeStamp.getTime() < $clock.getCurrentTime() - 60*60*1000,  $m: message, $machineId: machineId )
       	not(LogEntry(message.equals("Uklonjen virus"),machineId == $machineId, this after[0s, 1h] $l1))
        //not (AlarmTriggered(customerId == $ip, type == AlarmTriggered.VIRUS))
        
    then
    	alarmService.save(new AlarmTriggered($machineId, "Virus nije uklonjen", AlarmTriggered.VIRUS));
        System.out.println("Alarm: Virus nije uklonjen sa masine " + $machineId);
    	insert(new AlarmTriggered($machineId, "Nije uklonjen virus sa masine " + $machineId, AlarmTriggered.VIRUS));
end
rule "Vise od 7 pretnji virusa za isti ip"
    when
        $l1: LogEntry(message.equals("Virus"),  $m: message, $machineId: machineId)
        not (AlarmTriggered(customerId == $machineId, type == AlarmTriggered.POJAVLJIVANJE_VIRUSA))
        Number(intValue >= 6) from accumulate(
            $l2: LogEntry(
                this != $l1, 
                message == $m,
                machineId == $machineId,
                this after[0s, 10d] $l1
            ),
            count($l2)
        )
    then
    	alarmService.save(new AlarmTriggered($machineId, "Vise od 7 pretnji atnivisrusa u roku od 7 dana", AlarmTriggered.POJAVLJIVANJE_VIRUSA));
        System.out.println("Vise od 7 pretnji atnivisrusa u roku od 7 dana");
    	insert(new AlarmTriggered($machineId, "Vise od 7 pretnji atnivisrusa u roku od 7 dana" + $machineId, AlarmTriggered.POJAVLJIVANJE_VIRUSA));
end
rule "Pojava loga sa maliciozne ip adrese"
	when
		$l1 : LogEntry($ip:ipAddress)
		$mip : MaliciousIpAddress(ip.equals($ip))
	then
		alarmService.save(new AlarmTriggered($ip, "Pojava loga sa maliciozne ip adrese", AlarmTriggered.POJAVLJIVANJE_VIRUSA));
        System.out.println("Pojava loga sa maliciozne ip adrese");
    	insert(new AlarmTriggered($ip, "Pojava loga sa maliciozne ip adrese" + $ip, AlarmTriggered.POJAVLJIVANJE_VIRUSA));
end
rule "Suvise ucestali napadi (50 u roku od 60s)"
	when
		$dos : Number() 
			from accumulate ($l1 : LogEntry(category != "payment", message != "Neuspesna prijava na sistem") over window:time(60s), count($l1))
		$payment : Number() 
			from accumulate ($l2 : LogEntry(category == "payment") over window:time(60s), count($l2))
		$bruteForce : Number() 
			from accumulate ($l3 : LogEntry(category == "security related", message == "Neuspesna prijava na sistem") over window:time(60s), count($l3))
		eval($dos.intValue() + $payment.intValue() + $bruteForce.intValue() >= 50)
	then
		if ($bruteForce.intValue() * 5 >= $payment.intValue() * 3){
			if ($bruteForce.intValue() * 5 >= $dos.intValue()){
				System.out.println("Brute force napad");
				alarmService.save(new AlarmTriggered("1", "Brute force napad", AlarmTriggered.BRUTEFORCE_NAPAD));
    			insert(new AlarmTriggered("1", "Brute force napad", AlarmTriggered.BRUTEFORCE_NAPAD));
			}else{
				System.out.println("Dos napad");
				alarmService.save(new AlarmTriggered("1", "DoS napad", AlarmTriggered.DOS_NAPAD));
    			insert(new AlarmTriggered("1", "Dos napad", AlarmTriggered.DOS_NAPAD));
			}
		}else{
			if ($payment.intValue() * 3 >= $dos.intValue() ){
				alarmService.save(new AlarmTriggered("1", "Payment napad", AlarmTriggered.PAYMENT_NAPAD));
    			insert(new AlarmTriggered("1", "Payment napad", AlarmTriggered.PAYMENT_NAPAD));
				System.out.println("Payment napad");
			}else{
				System.out.println("Dos napad");
				alarmService.save(new AlarmTriggered("1", "DoS napad", AlarmTriggered.DOS_NAPAD));
    			insert(new AlarmTriggered("1", "Dos napad", AlarmTriggered.DOS_NAPAD));

			}
		}

end
rule "30 ili vise neuspesnih prijava na sistem sa iste ip adrese"
    when
        $l1: LogEntry(message.equals("Neuspesna prijava na sistem"), $ip: ipAddress, $m: message) 
        $broj : Number() from accumulate(
            $l2: LogEntry(
                this != $l1, 
                message == $m,
                ipAddress == $ip,
                this after[0s, 24h] $l1
            ),
            count($l2)
        )
        eval($broj.intValue() >= 30)
        
    then
        System.out.println("Malicious ip address " + $ip + " added to database");
        ipRepo.save(new MaliciousIpAddress($ip));
    	insert(new MaliciousIpAddress($ip));
end


